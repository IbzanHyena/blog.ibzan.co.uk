// title: Quick J: Thousands Separators
// date: 2023-05-14
// author: Ibzan

# Quick J: Thousands Separators

Often, we deal with big numbers.
Sometimes the numbers are represented in scientific notation, sometimes they are not.
When they are not, it can be challenging to gauge the size of a number of it is a single long string of digits:

```j
   NB. ! is factorial
   ! 20x
%%% ! 20x %%%
```

Wow!
That&rsquo;s hard to read.
Let&rsquo;s add some thousands separators.

## What are thousands separators?

If you don&rsquo;t know what they are, they are commas (or full stops/periods in other locales) which go between every three digits of a number.
This breaks the number up into groups of three that are easy to read.
You probably know this.

There are other systems in the world, too, such as [the Indian System][Indian Numbering], but I won&rsquo;t go into those here.

## Separating the Thousands

Let&rsquo;s consider the steps we will take to solve this problem:

- Convert our number to a string
- Break the string into consecutive groups of three, starting from the right
- Put commas between them

Should be simple!

### Convert our number to a string

J makes this easy &mdash; it&rsquo;s just [`":`&nbsp;default format][nuvoc-default-format].
Here&rsquo;s an example:

```j
   number =: %%%number =: 123456789%%%
   datatype number
%%% datatype number%%%
   ": number
%%% ": number %%%
   datatype ": number
%%% datatype ": number %%%
```

### Break the string into consecutive groups of three, starting from the right

This is where things start getting annoying.
J has several ways of manipulating arrays to extract subarrays, but none of them are quite perfect for this use case.
The issue is that we are starting from the _right_.
Our three best options, [`\`&nbsp;infix][nuvoc-infix], [`;.0`&nbsp;subarray][nuvoc-subarray], and [`;.3`&nbsp;subarrays][nuvoc-subarrays] all start from the _left_.
Dang.

One option could be to try and fashion indices to start from and take substrings, but an easier option is to reverse the string and then we are starting from the left.
To do this, we will use [`|.`&nbsp;reverse][nuvoc-reverse], [`&lt;`&nbsp;box][nuvoc-box], [`@`&nbsp;atop][nuvoc-atop], and [`\`&nbsp;infix][nuvoc-infix].

So, let&rsquo;s build this step-by-step.

Start by reversing the array:

```j
   |. ": number
%%% |. ": number %%%
```

Now we need the groups of three:

```j
   _3 &lt;\ |. ": number
%%% _3 <\ |. ": number %%%
```

Here, the negative part of `_3` (minus three) specifies to infix that we want non-overlapping groups.
If we had used `3` (positive three), the groups would have overlapped:

```j
   3 &lt;\ |. ": number
%%% 3 <\ |. ": number %%%
```

Also note that `\` is an adverb.
In J, all adverbs need a verb to act on.
Our verb here is `&lt;` box, which just takes an object and puts it in a box.
A box is like a pointer to something, and since J arrays have to be flat and homogenous, it can be used to create an equivalent of jagged or heterogenous arrays.

However, it would be better if the substrings were not reversed.
So, we can compose in another reversal to swap each group around into the right way:

```j
   &lt;@|. 'asdf'
%%% <@|. 'asdf' %%%
   _3 <@|.\ |. ": number
%%% _3 <@|.\ |. ": number %%%
```

### Put commas between them

This should be easy!
We can take advantage of another J adverb here, [`/`&nbsp;insert][nuvoc-insert].
Insert _inserts_ the verb it modifies between elements of an array.
So, as a basic example, `+/ 1 2 3` is the same as `1 + 2 + 3`.

For our application, we need the verb to be &ldquo;concatenate and put a comma in between&rdquo;: aka, a few uses of [`,`&nbsp;append][nuvoc-append] which appends arrays.

```j
   '123' , ',' , '456'
%%% '123' , ',' , '456' %%%
```

But wait!
Our groups are in reverse order, so we actually want to swap which way around they get combined.

```j
   '456' {{ y , ',' , x }} '123'
%%% '456' {{ y , ',' , x }} '123' %%%
```

J language aficionados may recognise the form `x u v y` as a [hook][nuvoc-hook], which is a way of writing this verb in a tacit (point-free) way.
It will also require us to use [`&amp;`&nbsp;bond][nuvoc-bond] however, which is like partial application.

```j
   prependComma =: ','&amp;,%%%prependComma =: ','&,%%%
   prependComma '123'
%%% prependComma '123' %%%
   '456' {{ y , prependComma x }} '123'
%%% '456' {{ y , prependComma x }} '123' %%%
   '456' (, prependComma) '123'
%%% '456' (, prependComma) '123' %%%
   '456' (, ','&amp;,) '123'
%%% '456' (, ','&,) '123' %%%
```

When we made the hook, we lost the swapped arguments &mdash; let&rsquo;s get them back!
There&rsquo;s a J adverb for this, and it&rsquo;s [`~`&nbsp;reflex][nuvoc-reflex].
It swaps arguments.

```j
   '456' (, ','&amp;,)~ '123'
%%% '456' (, ','&,)~ '123' %%%
```

Now all that&rsquo;s left is making it work on the boxed list we have.
To do that, we just need to unbox our operands first.

There&rsquo;s another J composition conjuction for this, too!
It&rsquo;s [`&amp;:`&nbsp;appose][nuvoc-appose], which takes two verbs.

It applies the right-hand verb to each operand separately, and then combines them with the left-hand operand.
Here&rsquo;s a quick diagram to try and show what I mean.

<svg xmlns="http://www.w3.org/2000/svg" xmlns:xlink="http://www.w3.org/1999/xlink" class="code-diagram">
  <text x="50%" y="10%" text-anchor="middle">x u&amp;:v y</text>
  <text x="25%" y="30%" text-anchor="middle">x</text>
  <text x="75%" y="30%" text-anchor="middle">y</text>
  <text x="25%" y="60%" text-anchor="middle">v</text>
  <text x="75%" y="60%" text-anchor="middle">v</text>
  <text x="50%" y="90%" text-anchor="middle">u</text>
  <line x1="25%" x2="25%" y1="35%" y2="50%"/>
  <line x1="75%" x2="75%" y1="35%" y2="50%"/>
  <line x1="25%" x2="48%" y1="65%" y2="80%"/>
  <line x1="75%" x2="52%" y1="65%" y2="80%"/>
</svg>

Unboxing is as simple as [`&gt;`&nbsp;open][nuvoc-open].
So, composing with this, and combining with the previous steps, we have:

```j
   number
%%% number %%%
   (, ','&amp;,)~&amp;:&gt;/ _3 &lt;@:|.\ |. ": number
%%% (, ','&,)~&:>/ _3 <@:|.\ |. ": number %%%
```

And that&rsquo;s everything!
The verb can be packaged up into a direct definition (cleaner than tacit in this case) as follows:


```j
   thousandsSeparate =: {{ (, ','&amp;,)~&amp;:&gt;/ _3 &lt;@:|.\ |. ": y }}%%% thousandsSeparate =: {{ (, ','&,)~&:>/ _3 <@:|.\ |. ": y }} %%%
   ! 20x
%%% ! 20x %%%
   thousandsSeparate ! 20x
%%% thousandsSeparate ! 20x %%%
```

That&rsquo;s all for today!
See you &rsquo;round for the next time I decide to write a quick blog post about a piece of J I write randomly.

[Indian Numbering]: https://en.wikipedia.org/wiki/Indian_numbering_system
[nuvoc-append]: https://code.jsoftware.com/wiki/Vocabulary/comma#dyadic
[nuvoc-appose]: https://code.jsoftware.com/wiki/Vocabulary/ampco
[nuvoc-atop]: https://code.jsoftware.com/wiki/Vocabulary/at
[nuvoc-bond]: https://code.jsoftware.com/wiki/Vocabulary/ampm
[nuvoc-box]: https://code.jsoftware.com/wiki/Vocabulary/lt
[nuvoc-default-format]: https://code.jsoftware.com/wiki/Vocabulary/quoteco
[nuvoc-hook]: https://code.jsoftware.com/wiki/Vocabulary/hook
[nuvoc-infix]: https://code.jsoftware.com/wiki/Vocabulary/bslash#dyadic
[nuvoc-insert]: https://code.jsoftware.com/wiki/Vocabulary/slash
[nuvoc-open]: https://code.jsoftware.com/wiki/Vocabulary/gt
[nuvoc-reflex]: https://code.jsoftware.com/wiki/Vocabulary/tilde
[nuvoc-reverse]: https://code.jsoftware.com/wiki/Vocabulary/bardot
[nuvoc-subarray]: https://code.jsoftware.com/wiki/Vocabulary/semidot0#dyadic
[nuvoc-subarrays]: https://code.jsoftware.com/wiki/Vocabulary/semidot3#dyadic
